/-
Copyright (c) 2025 Lean FRO, LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sofia Rodrigues
-/
module

prelude
public import Std.Internal.Http.Data.Body.Basic
public import Std.Internal.Http.Data.Body.Replayable

public section

/-!
# Body.Any

A type-erased body backed by closures. Implements `Http.Body` and can be constructed from any
type that also implements `Http.Body`. Used as the default handler response body type.
-/

namespace Std.Http.Body
open Std Internal IO Async

set_option linter.all true

/--
A type-erased body handle. Operations are stored as closures, making it open to any body type
that implements `Http.Body`.
-/
structure Any where

  /--
  Receives the next body chunk. Returns `none` at end-of-stream.
  -/
  recv : Async (Option Chunk)

  /--
  Closes the body stream.
  -/
  close : Async Unit

  /--
  Returns `true` when the body stream is closed.
  -/
  isClosed : Async Bool

  /--
  Selector that resolves when a chunk is available or EOF is reached.
  -/
  recvSelector : Selector (Option Chunk)

  /--
  Returns the declared size.
  -/
  getKnownSize : Async (Option Body.Length)

  /--
  Sets the size of the body.
  -/
  setKnownSize : Option Body.Length → Async Unit

  /--
  `true` when this body can be re-read after being consumed.
  Set by `Any.ofReplayableBody`; `false` for non-replayable bodies (e.g. `Body.Stream`).
  The HTTP client uses this to decide whether to follow 307/308 redirects.
  -/
  isReplayable : Bool := false

  /--
  Resets this body's read state so it can be re-read from the start.
  Only meaningful when `isReplayable = true`. No-op for `Body.Full` (always re-readable);
  resets the internal cursor for `Body.Buffered`.
  -/
  resetInPlace : Async Unit := pure ()

namespace Any

/--
Erases a body of any `Http.Body` instance into a `Body.Any`.
The resulting body has `isReplayable = false`.
-/
def ofBody [Http.Body α] (body : α) : Any where
  recv        := Http.Body.recv body
  close       := Http.Body.close body
  isClosed    := Http.Body.isClosed body
  recvSelector := Http.Body.recvSelector body
  getKnownSize := Http.Body.getKnownSize body
  setKnownSize := Http.Body.setKnownSize body

/--
Erases a replayable body into a `Body.Any`, preserving replay capability.
Sets `isReplayable = true` and `resetInPlace` from `Replayable.resetInPlace`.
-/
def ofReplayableBody [Http.Body α] [Replayable α] (body : α) : Any :=
  { ofBody body with
    isReplayable := true
    resetInPlace := Replayable.resetInPlace body }

end Any

instance : Http.Body Any where
  recv := Any.recv
  close := Any.close
  isClosed := Any.isClosed
  recvSelector := Any.recvSelector
  getKnownSize := Any.getKnownSize
  setKnownSize := Any.setKnownSize

end Std.Http.Body
