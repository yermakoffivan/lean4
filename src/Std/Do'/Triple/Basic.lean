/-
Copyright (c) 2025 Lean FRO LLC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vladimir Gladshtein, Sebastian Graf
-/
module

prelude
public import Std.Do'.WP

set_option linter.missingDocs true

@[expose] public section

open Lean.Order

/-!
# Hoare triples

Hoare triples form the basis for compositional functional correctness proofs about monadic programs.

As usual, `Triple pre x post epost` holds iff the precondition `pre` entails the weakest
precondition `wp x post epost` of `x : m α` for the postcondition `post` and error
postcondition `epost`.
It is thus defined in terms of an instance `WPMonad m l e`.
-/

namespace Std.Do'

universe u v
variable {m : Type u → Type v} {l : Type u} {e : Type u}

/-- A Hoare triple for reasoning about monadic programs. A Hoare triple `Triple pre x post epost`
is a *specification* for `x`: if assertion `pre` holds before `x`, then postcondition `post` holds
after running `x` (and `epost` handles any errors). -/
inductive Triple [Monad m] [WPMonad m l e] (pre : l) (x : m α) (post : α → l) (epost : e) : Prop
  /-- Construct a triple from a weakest precondition entailment. -/
  | intro (hwp : pre ⊑ wp x post epost)

/-- Hoare triple notation without exception postcondition (defaults to `⊥`). -/
notation:60 "⦃ " pre " ⦄ " x " ⦃ " post " ⦄" => Triple pre x post ⊥
/-- Hoare triple notation with a binder for the return value. -/
notation:60 "⦃ " pre " ⦄ " x " ⦃ " v ", " post " ⦄" => Triple pre x (fun v => post) ⊥
namespace Triple

variable [Monad m] [WPMonad m l e]

theorem iff {x : m α} {pre : l} {post : α → l} {epost : e} :
    Triple pre x post epost ↔ (pre ⊑ wp x post epost) :=
  ⟨fun ⟨h⟩ => h, fun h => ⟨h⟩⟩

theorem iff_conseq {x : m α} {pre : l} {post : α → l} {epost : e} :
    Triple pre x post epost ↔
    (∀ pre' post', (pre' ⊑ pre) → (post ⊑ post') → pre' ⊑ wp x post' epost) := by
  constructor
  · intro ⟨h⟩ pre' post' hpre hpost
    exact PartialOrder.rel_trans hpre (PartialOrder.rel_trans h (WPMonad.wp_cons x _ _ epost hpost))
  · intro h
    exact ⟨h _ _ PartialOrder.rel_refl (fun _ => PartialOrder.rel_refl)⟩

theorem entails_wp_of_pre_post {x : m α} {pre pre' : l} {post post' : α → l} {epost : e}
    (h : Triple pre' x post' epost) (hpre : pre ⊑ pre') (hpost : post' ⊑ post) :
    pre ⊑ wp x post epost :=
  iff_conseq.mp h _ _ hpre hpost

theorem entails_wp_of_pre {x : m α} {pre pre' : l} {post : α → l} {epost : e}
    (h : Triple pre' x post epost) (hpre : pre ⊑ pre') :
    pre ⊑ wp x post epost :=
  iff_conseq.mp h _ _ hpre (fun _ => PartialOrder.rel_refl)

theorem entails_wp_of_post {x : m α} {pre : l} {post post' : α → l} {epost : e}
    (h : Triple pre x post' epost) (hpost : post' ⊑ post) :
    pre ⊑ wp x post epost :=
  iff_conseq.mp h _ _ PartialOrder.rel_refl hpost

theorem pure (a : α) (h : pre ⊑ post a) :
    Triple pre (pure (f := m) a) post epost :=
  iff.mpr (PartialOrder.rel_trans h (WPMonad.wp_pure a post epost))

theorem bind (x : m α) (f : α → m β)
    (mid : α → l)
    (hx : Triple pre x mid epost)
    (hf : ∀ a, Triple (mid a) (f a) post epost) :
    Triple pre (x >>= f) post epost := by
  apply iff.mpr
  apply PartialOrder.rel_trans (iff.mp hx)
  apply PartialOrder.rel_trans (WPMonad.wp_cons x mid (fun a => wp (f a) post epost) epost
    (fun a => iff.mp (hf a)))
  exact WPMonad.wp_bind x f post epost

end Triple

end Std.Do'
